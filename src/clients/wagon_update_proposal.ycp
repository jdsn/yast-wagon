/**
 * File:
 *	clients/wagon_update_proposal.ycp
 *
 * Module:
 *	Wagon
 *
 * Authors:
 *	Lukas Ocilka <locilka@suse.cz>
 *
 * Summary:
 *	Online Migration Tool
 *
 * $Id$
 *
 */

{
    textdomain "wagon";

    import "Wagon";
    import "Report";
    import "PackageInstallation";

    string func = (string) WFM::Args(0);
    map param = (map) WFM::Args(1);
    map <string, any> ret = $[];

    if (func == "MakeProposal") {
	// Make sure the packager is initialized
	Wagon::InitPkg();
	// BNC #585095: All repositories (enabled/disabled) repositories
	// logged with their state.
	Wagon::LogAllRepos();

	boolean force_reset      = param["force_reset"     ]:false;
	boolean language_changed = param["language_changed"]:false;

	if (force_reset) {
	    Wagon::ResetDUPProposal();
	}

	Wagon::ProposeDUP();
	Wagon::ProposeDownloadMode();

	ret = Wagon::ProposalSummary();

    } else if (func == "AskUser") {
	string chosen_id = param["chosen_id"]:"";

	// toggle the download mode status
	if (chosen_id == Wagon::GetDownloadModeLink())
	{
	    PackageInstallation::SetDownloadInAdvance(!PackageInstallation::DownloadInAdvance());
	}
	else
	{
	    Report::Message (_("There is nothing to set."));
	}

	ret = $[ "workflow_sequence" : `next ];

    } else if (func == "Description") {
	ret = $[
	    "rich_text_title"	:	_("Update Options"),
	    "menu_title"	:	_("&Update Options"),
	    "id"		:	"wagon_update_proposal",
	];
    }

    return ret;
}
